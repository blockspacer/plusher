toolchain("clang_toolchain") {
  ar = "ar"

  tool("cxx") {
    command = "clang++ {{defines}} {{include_dirs}} {{cflags}} -c {{source}} -o {{output}}"
    description = "CXX {{output}}"
    outputs = [
      "{{source_out_dir}}/{{source_name_part}}.o",
    ]
  }

  tool("link") {
    outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    command = "clang++ {{ldflags}} {{solibs}} {{libs}} -o \"$outfile\" {{inputs}}"
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    outputs = [
      outfile,
    ]

    lib_switch = "-l"
    lib_dir_switch = "-L"
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("alink") {
    command = "rm -f {{output}} && $ar {{arflags}} rcs {{output}} {{inputs}}"
    description = "AR {{output}}"
    outputs = [
      "{{output_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    default_output_dir = "{{target_out_dir}}"
    default_output_extension = ".a"
    output_prefix = "lib"
  }
}
